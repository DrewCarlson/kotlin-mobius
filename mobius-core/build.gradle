apply plugin: 'kotlin-multiplatform'
apply plugin: 'org.jetbrains.dokka'
apply from: "$rootDir/gradle/publish.gradle"

kotlin {
  jvm()
  js {
    nodejs()
    browser  {
      testTask {
        useKarma {
          useChromeHeadless()
        }
      }
    }
    compilations.main {
      compileKotlinJs.kotlinOptions {
        sourceMap = true
        metaInfo = true
        moduleKind = 'umd'
        sourceMapEmbedSources = 'always'
      }
      compileTestKotlinJs.kotlinOptions {
        moduleKind = 'umd'
        sourceMapEmbedSources = 'always'
      }
    }
  }
  configure([
      iosX64(), iosArm32(), iosArm64(),
      macosX64(), linuxX64(), mingwX64(),
      linuxMipsel32(), linuxArm32Hfp(), linuxMips32(),
      wasm32(), androidNativeArm32(), androidNativeArm64()
  ]) {
    compilations.main.defaultSourceSet {
      kotlin.srcDirs = ['src/nativeMain/kotlin']
    }
  }
}

dependencies {
  commonMainImplementation "org.jetbrains.kotlin:kotlin-stdlib-common"
  commonTestImplementation "org.jetbrains.kotlin:kotlin-test-annotations-common"
  commonTestImplementation "org.jetbrains.kotlin:kotlin-test-common"

  jsMainImplementation "org.jetbrains.kotlin:kotlin-stdlib-js"
  jsTestImplementation "org.jetbrains.kotlin:kotlin-test-js"

  jvmMainImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
  jvmTestImplementation "org.jetbrains.kotlin:kotlin-test"
  jvmTestImplementation "org.jetbrains.kotlin:kotlin-test-junit"
  jvmTestImplementation "com.google.guava:guava:$guava_version"
  jvmTestImplementation "org.awaitility:awaitility:$awaitality_version"
}

dokka {
  outputFormat = 'html'
  outputDirectory = "$buildDir/javadoc"
}

task publishIos {
  dependsOn(
      'publishIosX64PublicationToMavenRepository',
      'publishIosArm64PublicationToMavenRepository',
      'publishIosArm32PublicationToMavenRepository'
  )
}

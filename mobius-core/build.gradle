apply plugin: 'kotlin-multiplatform'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'com.moowork.node'
apply from: "$rootDir/gradle/publish.gradle"

kotlin {
  targets {
    fromPreset(presets.iosX64, 'iosX64')
    fromPreset(presets.iosArm32, 'iosArm32')
    fromPreset(presets.iosArm64, 'iosArm64')
    fromPreset(presets.macosX64, 'macosX64')
    fromPreset(presets.linuxX64, 'linuxX64')
    fromPreset(presets.mingwX64, 'mingwX64')
    fromPreset(presets.wasm32, 'wasm32')
    fromPreset(presets.linuxArm32Hfp, 'linuxArm32Hfp')
    fromPreset(presets.linuxMips32, 'linuxMips32')
    fromPreset(presets.linuxMipsel32, 'linuxMipsel32')
    fromPreset(presets.androidNativeArm32, 'androidNativeArm32')
    fromPreset(presets.androidNativeArm64, 'androidNativeArm64')
    fromPreset(presets.jvm, 'jvm')
    fromPreset(presets.js, 'js') {
      compilations.main {
        compileKotlinJs.kotlinOptions {
          sourceMap = true
          metaInfo = true
          moduleKind = 'umd'
          outputFile = "$buildDir/classes/kotlin/js/test/mobius-core.js"
        }
        compileTestKotlinJs.kotlinOptions {
          moduleKind = 'commonjs'
        }
      }
    }
  }
  sourceSets {
    configure([
        iosX64Main, iosArm32Main, iosArm64Main,
        macosX64Main, linuxX64Main, mingwX64Main,
        linuxMipsel32Main, linuxArm32HfpMain, linuxMips32Main,
        wasm32Main, androidNativeArm32Main, androidNativeArm64Main
    ]) {
      kotlin.srcDirs += 'src/nativeMain/kotlin'
    }
  }
}

dependencies {
  commonMainImplementation "org.jetbrains.kotlin:kotlin-stdlib-common"
  commonTestImplementation "org.jetbrains.kotlin:kotlin-test-annotations-common"
  commonTestImplementation "org.jetbrains.kotlin:kotlin-test-common"

  jsMainImplementation "org.jetbrains.kotlin:kotlin-stdlib-js"
  jsTestImplementation "org.jetbrains.kotlin:kotlin-test-js"

  jvmMainImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
  jvmTestImplementation "org.jetbrains.kotlin:kotlin-test"
  jvmTestImplementation "org.jetbrains.kotlin:kotlin-test-junit"
  jvmTestImplementation "com.google.guava:guava:$guava_version"
  jvmTestImplementation "org.awaitility:awaitility:$awaitality_version"
}

dokka {
  outputFormat = 'html'
  outputDirectory = "$buildDir/javadoc"
}

node {
  version = node_version
  workDir = file("$rootDir/.gradle/nodejs")
  yarnWorkDir = file("$rootDir/.gradle/yarn")
  download = true
  nodeModulesDir = file("$buildDir/yarn")
}

def jsCompilations = kotlin.targets.js.compilations
task populateNodeModules {
  doLast {
    copy {
      from "$buildDir/yarn/node_modules"
      from jsCompilations.main.output.allOutputs
      jsCompilations.test.runtimeDependencyFiles.each {
        if (it.exists() && !it.isDirectory()) {
          from zipTree(it.absolutePath).matching { include '*.js' }
        }
      }
      into "$buildDir/node_modules"
    }
  }
}

task addYarnDeps(type: YarnTask) {
  doFirst {
    mkdir "$buildDir/yarn"
  }
  args = ["add", "qunit@$qunit_version"]
}

task runQunit(type: NodeTask, dependsOn: [jsCompilations.test.compileKotlinTaskName, addYarnDeps, populateNodeModules]) {
  script = file("$buildDir/node_modules/qunit/bin/qunit")
  args = [relativePath("${jsCompilations.test.output.classesDirs.first()}/mobius-core_test.js")]
}

jsTest.dependsOn runQunit

// Fix for https://github.com/srs/gradle-node-plugin/issues/301
repositories.whenObjectAdded {
  if (it instanceof IvyArtifactRepository) {
    metadataSources {
      artifact()
    }
  }
}

task publishIos {
  dependsOn(
      'publishIosX64PublicationToMavenRepository',
      'publishIosArm64PublicationToMavenRepository',
      'publishIosArm32PublicationToMavenRepository'
  )
}

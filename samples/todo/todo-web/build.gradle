apply plugin: 'kotlin-platform-js'
apply plugin: 'kotlin2js'

dependencies {
  expectedBy project(":samples:todo:todo-common")
  compile "org.jetbrains.kotlin:kotlin-stdlib-js"
  compile project(":common:core-common")
  testCompile "org.jetbrains.kotlin:kotlin-test-js"
}

compileKotlin2Js {
  kotlinOptions.outputFile = "${projectDir}/web/output.js"
  kotlinOptions.moduleKind = "plain"
  kotlinOptions.sourceMap = true
}

//noinspection GroovyAssignabilityCheck
task assembleWeb(type: Sync) {
  configurations.compile.each { File file ->
    from(zipTree(file.absolutePath), {
      includeEmptyDirs = false
      include { fileTreeElement ->
        def path = fileTreeElement.path
        path.endsWith(".js") && (path.startsWith("META-INF/resources/") ||
            !path.startsWith("META-INF/"))
      }
    })
  }
  from compileKotlin2Js.destinationDir
  into "${projectDir}/build/web"

  dependsOn classes
}

assemble.dependsOn assembleWeb
